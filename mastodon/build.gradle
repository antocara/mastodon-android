plugins {
    id 'com.android.application'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 33
    defaultConfig {
        applicationId "org.joinmastodon.android"
        minSdk 23
        targetSdk 33
        versionCode 48
        versionName "1.1.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "ar-rSA", "be-rBY", "bn-rBD", "bs-rBA", "ca-rES", "cs-rCZ", "de-rDE", "el-rGR", "es-rES", "eu-rES", "fi-rFI", "fil-rPH", "fr-rFR", "ga-rIE", "gd-rGB", "gl-rES", "hi-rIN", "hr-rHR", "hu-rHU", "hy-rAM", "in-rID", "is-rIS", "it-rIT", "iw-rIL", "ja-rJP", "kab", "ko-rKR", "nl-rNL", "oc-rFR", "pl-rPL", "pt-rBR", "pt-rPT", "ro-rRO", "ru-rRU", "si-rLK", "sl-rSI", "sv-rSE", "th-rTH", "tr-rTR", "uk-rUA", "vi-rVN", "zh-rCN", "zh-rTW"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
        appcenterPrivateBeta {
            initWith release
            minifyEnabled false
            shrinkResources false
            versionNameSuffix "-priv-beta"
        }
        appcenterPublicBeta {
            initWith release
            versionNameSuffix "-beta"
        }
        githubRelease {
            initWith release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }
    sourceSets {
        appcenterPrivateBeta {
            setRoot "src/appcenter"
        }
        appcenterPublicBeta {
            setRoot "src/appcenter"
        }
        githubRelease {
            setRoot "src/github"
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    api 'androidx.annotation:annotation:1.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.jsoup:jsoup:1.14.3'
    implementation 'com.squareup:otto:1.3.8'
    implementation 'de.psdev:async-otto:1.0.3'
    implementation 'org.parceler:parceler-api:1.1.12'

    // kotlin
    implementation('androidx.core:core-ktx:+') {
        exclude group: 'androidx.collection', module: 'collection'
        exclude group: 'androidx.concurrent', module: 'concurrent-futures'
    }

    implementation('me.grishka.litex:recyclerview:1.2.1.1')
    implementation('me.grishka.litex:swiperefreshlayout:1.1.0.1')
    implementation('me.grishka.litex:browser:1.4.0')
    implementation('me.grishka.litex:dynamicanimation:1.1.0-alpha03')
    implementation('me.grishka.litex:viewpager:1.0.0')
    implementation('me.grishka.litex:viewpager2:1.0.0')
    implementation('me.grishka.appkit:appkit:1.2.7')
    implementation('me.grishka.litex:collection:1.1.0')

    //KOIN
    def koin_android_version = "3.3.3"
    def koin_android_compose_version = "3.4.2"
    def koin_core_version = "3.3.3"
    implementation "io.insert-koin:koin-core:$koin_core_version"
    implementation("io.insert-koin:koin-android:$koin_android_version") {
        exclude group: 'androidx.collection', module: 'collection'
        exclude group: 'androidx.concurrent', module: 'concurrent-futures'
        exclude group: 'androidx.viewpager', module: 'viewpager'
    }
    // implementation "io.insert-koin:koin-androidx-compose:$koin_android_compose_version"

    //COROUTINES
    def lifecycle_version = "2.5.1"
    // Lifecycles only (without ViewModel or LiveData)
    implementation ("androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version") {
        exclude group: 'androidx.collection', module: 'collection'
        exclude group: 'androidx.concurrent', module: 'concurrent-futures'
    }
    // ViewModel
    implementation ("androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"){
        exclude group: 'androidx.collection', module: 'collection'
        exclude group: 'androidx.concurrent', module: 'concurrent-futures'
    }
    // ViewModel utilities for Compose
    implementation ("androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"){
        exclude group: 'androidx.collection', module: 'collection'
        exclude group: 'androidx.concurrent', module: 'concurrent-futures'
    }


    annotationProcessor 'org.parceler:parceler:1.1.12'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    def appCenterSdkVersion = "4.4.2"
    appcenterPrivateBetaImplementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    appcenterPrivateBetaImplementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"
    appcenterPublicBetaImplementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    appcenterPublicBetaImplementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"

    // ANDROID TEST
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'

    // Unit test
    testImplementation("junit:junit:4.13.2")


}
